import com.github.rjeschke.txtmark.*
import org.apache.tools.ant.filters.*

apply plugin: "base"
apply plugin: "groovy"
apply plugin: "java"

version =  "1.0.0"

if (hasProperty('conf')) {
    System.err.print "Using configuration data from ${conf}"
    File confFile = new File(conf)
    if (! confFile.exists()) {
        throw new Exception("No publication configuration file ${conf} found.")
    } else {
        apply from: conf
    }

} else {
    File confFile = new File("conf.gradle")
    if (! confFile.exists()) {
        throw new Exception("No publication configuration file ${confFile} found\
.")
    } else {
        println "Using default configuration in 'conf.gradle'"
        apply from: "conf.gradle"
    }
}

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath group: 'com.github.rjeschke', name: 'txtmark', version: '0.11'
  }
}


repositories {
    mavenCentral()
}

dependencies {
  compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.0.6'

  testCompile 'junit:junit:4.11'
  testCompile 'org.concordion:concordion:1.4.4'

}


task copyMarkdown(type: Copy) {
  description "Copies markdown source into build area for further processing"
  from "writing"
  into "${buildDir}/writing"
}


test.dependsOn  copyMarkdown
test {
    systemProperties 'concordion.output.dir': file("${buildDir}/concordion-results")
}

// file system layout for testing with concordion:
sourceSets {
    test {
        java {
	  srcDir "${javaSrc}"
        }
        resources {
	  srcDir "${buildDir}/writing"
        }
    }
}

task setUpConcordion(dependsOn: copyMarkdown) {
  description = "Converts markdown source into HTML concordion can test"
}
setUpConcordion.doLast {
  FileTree tree = fileTree(mdSrc) {
    include "**/*.md"
  }
  tree.visit { f ->
    if (f.relativePath.isFile()) {
      File inFile = new File("${mdSrc}/${f.relativePath}")
      def segs = f.relativePath.getSegments()
      String treePath = "${buildDir}/writing"
      Integer limit =  segs.size() - 1
      segs.eachWithIndex { s, i ->
	if (i < limit) {
	  treePath = "${treePath}/${s}"
	  File nxtDir = new File(treePath)
	  if (! nxtDir.exists()) {
	    nxtDir.mkdir()
	  }
	}
      }
      File outDir = new File(treePath)
      String htmlFileName = f.relativePath.getLastName().replaceFirst(/.md$/,".html")
      File htmlFile = new File(outDir, htmlFileName)
      String body = Processor.process(inFile.getText("UTF-8"),Configuration.DEFAULT)
      htmlFile.setText("${htmlPreface}${body}${htmlEnd}", "UTF-8")
    }
  }
}


test.dependsOn setUpConcordion
test {
    systemProperties 'concordion.output.dir': file("${buildDir}/concordion-results")
}

task addVersion(){
  description "Adds project version to list of filterable tokens"
}
addVersion.doLast {
  tokenMap["version"] = version
}

task conc(type: Copy, dependsOn: [test, addVersion]) {
  description "Runs concordion tests on markdown source"
  from "${buildDir}/concordion-results"
  into "${buildDir}/concordion-formatted"
  filter(ReplaceTokens, tokens: tokenMap)
}

conc.doLast {
  println "\nFormatted output is in ${buildDir}/concordion-formatted"
}